apply from: "../gradle/shared.gradle"

apply plugin: 'com.palantir.java-distribution'
apply plugin: 'nu.studer.jooq'
apply plugin: 'org.inferred.processors'
apply plugin: 'org.unbroken-dome.test-sets'

testSets {
    integTest
}

jacocoTestReport {
    executionData test, integTest
}

check.dependsOn integTest

dependencies {
    compile project(":timestamp-api")
    compile project(":lock-impl")
    compile project(":leader-election-impl")
    compile project(":atlasdb-config")

    compile group: 'com.github.rholder', name: 'guava-retrying'
    compile group: 'com.palantir.remoting1', name: 'jersey-servers'
    compile group: 'com.palantir.remoting1', name: 'ssl-config'
    compile group: 'io.atomix', name: 'atomix'
    compile group: 'io.atomix.catalyst', name: 'catalyst-netty'
    compile group: 'com.hazelcast', name: 'hazelcast-all', version: '3.7.3'
    compile group: 'org.jooq', name: 'jooq', version: '3.8.6'
    compile group: 'org.jooq', name: 'jooq-meta', version: '3.8.6'
    compile group: 'org.jooq', name: 'jooq-codegen', version: '3.8.6'
    compile group: 'io.dropwizard', name: 'dropwizard-core'

    compile(group: 'org.jsimpledb', name: 'jsimpledb-main', version: '2.4.5') {
        exclude module: 'slf4j-log4j12'
    }
    compile(group: 'org.jsimpledb', name: 'jsimpledb-kv-raft', version: '2.4.5') {
        exclude module: 'slf4j-log4j12'
    }
    compile(group: 'org.jsimpledb', name: 'jsimpledb-kv-sqlite', version: '2.4.5') {
        exclude module: 'slf4j-log4j12'
    }

    runtime group: 'org.xerial', name: 'sqlite-jdbc', version: '3.15.1'
    jooqRuntime group: 'org.xerial', name: 'sqlite-jdbc', version: '3.15.1'

    processor group: 'org.immutables', name: 'value'

    testCompile project(':atlasdb-config')

    testCompile group: 'org.assertj', name: 'assertj-core'
    testCompile group: 'org.mockito', name: 'mockito-core'

    integTestCompile group: 'io.dropwizard', name: 'dropwizard-testing'
}

jooq {
    timestamp(sourceSets.main) {
        jdbc {
            driver = 'org.sqlite.JDBC'
            url = "jdbc:sqlite:$projectDir/timestamps.sqlite"
            user = ''
            password = ''
        }
        generator {
            strategy {
                name = 'org.jooq.util.DefaultGeneratorStrategy'
            }
            database {
                name = 'org.jooq.util.sqlite.SQLiteDatabase'
                // Sname = 'org.jooq.util.postgres.PostgresDatabase'
            }
            target {
                packageName = 'com.palantir.atlasdb.timelock.hazelcast.generated'
                directory = 'src/main/java'
            }
        }
    }
}

distribution {
    serviceName 'atlasdb-timelock-server'
    mainClass 'com.palantir.atlasdb.timelock.TimeLockServer'
    args 'server', 'var/conf/timelock.yml'
}
