buildscript {
  repositories {
    jcenter()
  }
  dependencies {
    classpath group:'ch.raffael.gradlePlugins.antlr4', name:'gradle-antlr4-plugin', version:'1.0'
  }
}

apply plugin:'ch.raffael.antlr4'
apply plugin: "org.inferred.processors"

apply from: "../gradle/shared.gradle"

dependencies {
  compile(project(":atlasdb-commons"))
  compile(project(":atlasdb-api"))
  compile(project(":atlasdb-cli"))
  compile(project(":atlasdb-server"))
  compile "org.jooq:jooq:3.6.4"
  compile "org.antlr:antlr4:4.5"
  compile 'com.google.guava:guava:' + libVersions.guava
  processor "org.immutables:value:" + libVersions.immutables
  testCompile(project(":atlasdb-tests-shared"))
}

configurations.matching({ it.name in ['compile', 'runtime'] }).all {
      resolutionStrategy {
          force 'com.google.code.findbugs:jsr305:' + libVersions.jsr305
          force 'org.slf4j:slf4j-api:' + libVersions.slf4j
          force 'org.apache.commons:commons-lang3:' + libVersions.commons_lang3
          force 'org.apache.thrift:libthrift:' + libVersions.libthrift
          force 'ch.qos.logback:logback-classic:1.1.3'
          force 'commons-codec:commons-codec:' + libVersions.commons_codec
          force 'com.googlecode.json-simple:json-simple:1.1.1'
          force 'joda-time:joda-time:' + libVersions.joda_time
          force 'org.yaml:snakeyaml:1.12'
          force 'javax.validation:validation-api:1.1.0.Final'
      }
}

antlr4 {
  version = '4.5'
  visitor = true
  listener = false
  destination = file("$projectDir/src/main/java")
}

// generate the antlr source when building for an IDE
tasks.idea.dependsOn tasks.antlr4
tasks.eclipse.dependsOn tasks.antlr4

// make a single jar with all dependencies
task fatJar(type: Jar) {
  manifest {
    attributes 'Implementation-Title': 'AtlasDB SQL Driver',
               'Implementation-Version': rootProject.version
  }
  baseName = project.name + '-all'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
  with jar
}
