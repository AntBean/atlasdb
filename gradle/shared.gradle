apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'checkstyle'
apply plugin: 'maven'
apply plugin: 'maven-publish'
apply plugin: 'jacoco-coverage'

apply plugin: 'com.github.hierynomus.license'

sourceCompatibility = 1.7
targetCompatibility = 1.7

version = rootProject.version
group = rootProject.group

repositories {
    mavenCentral()
    maven {
        url 'http://dl.bintray.com/palantir/releases/'
    }
}

checkstyle {
    configFile rootProject.file('config/checkstyle/checkstyle.xml')
}

apply from: rootProject.file('gradle/versions.gradle')
libsDirName = file('build/artifacts')

dependencies {
    checkstyle 'com.puppycrawl.tools:checkstyle:6.6'
    testCompile group: 'junit', name: 'junit', version: libVersions.junit
}

apply from: rootProject.file('gradle/javadoc.gradle'), to: javadoc

license {
    header rootProject.file('config/HEADER')
    skipExistingHeaders true

    ext.year = Calendar.getInstance().get(Calendar.YEAR)

    include '**/*.java'
    exclude '**/generated/*.java'
}

task sourceJar(type: Jar) {
    from sourceSets.main.allSource
    classifier 'sources'
}

publishing {
    publications {
        local(MavenPublication) {
            from components.java

            artifact(sourceJar) {
                classifier 'sources'
            }
            pom.withXml {
                def scm = asNode().appendNode('scm')
                scm.appendNode('url', 'https://github.com/palantir/atlasdb')
                def licenses = asNode().appendNode('licenses')
                def license = licenses.appendNode('license')
                license.appendNode('name', 'BSD-3')
                license.appendNode('url', 'http://opensource.org/licenses/BSD-3-Clause')
            }
        }
    }

    repositories {
        maven {
            url localPublishDir
        }
    }
}
